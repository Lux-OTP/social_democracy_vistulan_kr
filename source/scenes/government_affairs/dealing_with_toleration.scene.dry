title: Dealing with Schleicher
new-page: true
is-card: true
card-image: img/portraits/BrÃ¼ningHeinrich.jpg
view-if: dealing_with_toleration_timer == 0 and chancellor == "Schleicher"
on-arrival: dealing_with_toleration_timer += 6; month_actions += 1
tags: govt_affairs
#subtitle: Despite not being in government, we can still provide support.

= Dealing with Schleicher

We are currently the strongest force opposing the government of Chancellor [+ chancellor +]. How should we use our position?

- @break_toleration
- @urge_against_austerity
- @improve_relations
- @easy_discard

@break_toleration
view-if: not constructive_vonc
title: End the Red General's rule with a vote of no confidence!
unavailable-subtitle: We don't have sufficient support in the Reichstag.
choose_if: z_relation >= 40 and lvp_relation >= 40 and minorit_bloc_relation >= 40 and agrarians_relation >= 40

Are we sure about this? A vote of no confidence could trigger new elections should we fail to find a majority, and as a result - potentially increase vote-share for the conservative parties.

- @vonc: Yes, end this antiparliamentarian farce!
- @stay_course: No, we can't take the risk just yet.

@vonc
on-arrival: spd_in_government = 0; spd_toleration = 0; in_grand_coalition = 0; schleicher_ousted = 1
go-to: election_1928.coalition_menu

We have called for a vote of no confidence, and brought down this government. Unless we can present a workable majority to [+ president +], new election will have to be called.

@stay_course

Whether it's because we don't want to join the government in the current state of affairs, or we don't have enough support in the parliament to fully succeed, we decide that we can't make such a fateful decision just yet.

@urge_against_austerity
title: Urge the chancellor to engage in talks with the striking Ruhr workers.
view-if: ruhrkampf == 1
choose-if: jungturken_strength <= 70 or (staatspolitiker_strength >= 40 and labor_strength >= 40)
unavailable-subtitle: The left wing of our party opposes any talks with the chancellor.
go-to: urge_success if radicalization <= 6; urge_failure if radicalization > 6

@urge_success
on-arrival: rfb_militancy -= 0.05; ruhrkampf_agreement += 1; workers_spd += 3*(1-dissent); unemployed_spd += 3*(1-dissent); jungturken_dissent -= 5; staatspolitiker_dissent -= 5; center_dissent -= 5; labor_dissent -= 5; z_relation += 5*(1-dissent); lvp_relation += 5*(1-dissent); minorit_bloc_relation += 5*(1-dissent); agrarians_relation += 5*(1-dissent)

We have successfully negotiated concessions for the workers striking in the Ruhr! This has slightly cooled the atmosphere of the demonstrations, showing 

@urge_failure
on-arrival: jungturken_strength += 5; labor_dissent += 5

Unfortunately, our negotiations have met with failure. The Schleicher cabinet sees us as merely a "different shade of syndicalist red" and doesn't want to listen to us.

@improve_relations
title: Improve relations with the parties of the opposition.
on-arrival: z_relation += 5*(1-dissent); lvp_relation += 5*(1-dissent); agrarians_relation += 5*(1-dissent); minorit_bloc_relation += 5*(1-dissent)

We have increased our level of contact and discussions with the parties of the opposition, urging them to cooperate against chancellor Schleicher.
