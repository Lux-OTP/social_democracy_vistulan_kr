title: Waning influence in Russia
subtitle: The russian government is meddling in our affairs.
new-page: true
on-arrival: economic_growth -= 0.5; inflation -= 0.5; russia_sanctions_seen = 1
tags: event
view-if: ((month == 8  and year == 1936) or (month == 6 and year == 1938)) and russia_sanctions_seen == 0 and sp_in_government == 1
max-visits: 2

= Waning influence in Russia

In response to economic turmoil caused in the Reichspakt by the Black Monday, Boris Savinkov, current [? if year == 1936: president?][? if year == 1937 or year == 1938: leader?] of Russia has tasked his administration with tackling the issue of german economic influence in Russia. Across the country, factories and infrastructure are being nationalized for symbolic compensation, worsening the already difficult situation of the German economy.

While we can't send our troops to handle the situation like during the fallout of Weltkrieg, something has to be done to address this blatant defiance of our hegemony.

-@options: Is there anything we can do to address this crisis?  

@options

- @russia_nothing_we_can_do: It's the government's issue.
- @russia_nothing_we_can_do_govt: We can't do much.
- @compensate: Compensate our investors and shareholders.
- @send_demands: We will threaten to use force in defence of our businesses.
- @mitteleuropa_sanctions: Call for Mitteleuropa to impose sanctions on Russia.

@russia_nothing_we_can_do
view-if: spd_in_government == 0
on-arrival: economic_growth -= 1.5; ostwall_strength -= 25; ostwall_loyalty -= 0.05; sa_militancy -= 0.10; sa_strength -= 50

[+ chancellor +]'s government answers by urging Mitteleuropa to impose an embargo on Russia. While this measure succeeds in weakening the russian economy, it doesn't help to ease the impact this crisis has on our economy. Additionally, most of the burden of the sanctions falls on our eastern european allies.

@russia_nothing_we_can_do_govt
view-if: spd_in_government == 1
on-arrival: economic_growth -= 1.5; inflation -= 0.2; new_middle_spd -= 3*(1-dissent); old_middle_spd -= 3*(1-dissent); workers_spd -= 3; unemployed_spd -= 3

We remain apathetic to the crisis. German-owned industries in Russia are no more, unlike the popular distaste caused by our inaction. 

@compensate
view-if: spd_in_government == 1
subtitle: -1 budget
choose-if: budget >= 1
on-arrival: budget -= 1; inflation += 0.2; economic_growth -= 1; new_middle_spd += 3*(1-dissent); old_middle_spd += 3*(1-dissent); workers_spd -= 3; unemployed_spd -= 3; jungturken_dissent += 5; center_dissent += 5

We provide compensation to the affected german entrepreneurs. However, the effect this action has on our economy is negligible, and many people see this as an admission of defeat and careless spending in the time of crisis.

@send_demands
view-if: spd_in_government == 1

We can threaten the Russians with using the Heer to enforce economic concessions we enjoy in their country. Our threats are nothing more than empty words however, as our army is still by no means ready for war and would likely outright refuse to prepare any offensive operations against Russia. This is a dangerous move which can easily backfire should our opponents see through our bluff.

- @send_demands_2: We can't show any weakness.
- @options: Actually, we should reconsider...

@send_demands_2
on-arrival: defense_strength = reichswehr_militancy*reichswehr_strength; defense_strength += ostwall_militancy*ostwall_strength; enemy_strength = sa_strength*sa_militancy + (sh_strength*sh_militancy)*0.2;
go-to: send_demands_victory if (defense_strength*0.6) > enemy_strength; send_demands_defeat if (0.6*defense_strength) <= enemy_strength

@send_demands_defeat
title: Russian [? if year == 1936: Republic?][? if year == 1937 or year == 1938: State?] stands firm.
on-arrival: sa_strength += 50; reichswehr_loyalty -= 0.05; ostwall_loyalty -= 0.05; economic_growth -= 1.5; old_middle_dkp += 4; new_middle_dkp += 4; old_middle_dvlp += 4; new_middle_dvlp += 4

Russian government doesn't seem too impressed by our threats, and their nationalization program continues unabated. They respond by orchestrating large scale exercises on the Reichspakt's eastern border. Both german people and Reichspakt allies are disappointed by our fiasco.

@send_demands_victory
title: German Empire stands victorious.
on-arrival: reichswehr_strength += 25; reichswehr_loyalty += 0.05; ostwall_loyalty += 0.05; new_middle_spd += 3*(1-dissent); old_middle_spd += 3*(1-dissent); workers_spd += 3*(1-dissent); unemployed_spd += 3*(1-dissent); sa_strength -= 50

Fearing the Heer's military might, the russian government quickly backs down from their nationalization program, rolling back most of the reforms aimed at german-owned enterprises in their country. The German Empire has emerged victorious in this show of strength.

@mitteleuropa_sanctions
unavailable-subtitle: Ukrainian opposition renders this motion impossible.
view-if: spd_in_government == 1
choose-if: ukraine_revolt == 0
on-arrival: ostwall_loyalty -= 0.1; inflation += 0.2; economic_growth -= 1.5; sa_militancy -= 0.05; sa_strength -= 50

We call for members of Mitteleuropa to impose sanctions on Russia. While the russian economy is noticeably shaken by our actions, our eastern european allies are forced to suffer most of the economic hardships. The sanctions are also detrimental to the already struggling german economy.